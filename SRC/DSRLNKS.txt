       REF  VSBR$
       DEF  DSR$
*
DREGS  BSS  >20
*
HEX20  BYTE >20
HEXAA  BYTE >AA
SAVE1  DATA 0
SAVE2  DATA 0
SAVE3  DATA 0
SAVE4  DATA 0
SAVE5  DATA 0
NAMBUF BSS  10
PERIOD BYTE '.'
       EVEN
DSR$   DATA DREGS,DSR1   * ULTRA STANDARD...
*
DSR1   LIMI  0           * SINCE R15 HAS USER'S INTERRUPT MASK ANYWAY...
       MOV   *R14+,R5
       SZCB  @HEX20,R15
       MOV   @>8356,R0
       MOV   R0,R9
       AI    R9,>FFF8
       BLWP  @VSBR$
       MOVB  R1,R3
       SRL   R3,>8
       SETO  R4
       LI    R2,NAMBUF
DLOOP1 INC   R0
       INC   R4
       C     R4,R3
       JEQ   DJUMP1
       BLWP  @VSBR$
       MOVB  R1,*R2+
       CB    R1,@PERIOD
       JEQ   DJUMP1
       CI    R2,NAMBUF+10
       JLT   DLOOP1
DJUMP1 MOV   R4,R4
       JEQ   DJUMP6
       CI    R4,>0007
       JGT   DJUMP6
       CLR   @>83D0
       MOV   R4,@>8354
       MOV   R4,@SAVE3
       INC   R4
       A     R4,@>8356
       MOV   @>8356,@SAVE4
       LWPI  >83E0
       CLR   R1
       LI    R12,>0F00
DLOOP2 MOV   R12,R12
       JEQ   DJUMP2
       SBZ   >00
DJUMP2 AI    R12,>0100
       CLR   @>83D0
       CI    R12,>2000
       JEQ   DJUMP5
       MOV   R12,@>83D0
       SBO   >00
       LI    R2,>4000
       CB    *R2,@HEXAA
       JNE   DLOOP2
       A     @5*2+DREGS,R2
       JMP   DJUMP3
DLOOP3 MOV   @>83D2,R2
       SBO   >00
DJUMP3 MOV   *R2,R2
       JEQ   DLOOP2
       MOV   R2,@>83D2
       INCT  R2
       MOV   *R2+,R9
       MOVB  @>8355,R5
       JEQ   DJUMP4
       CB    R5,*R2+
       JNE   DLOOP3
       SRL   R5,>8
       LI    R6,NAMBUF
DLOOP4 CB    *R6+,*R2+
       JNE   DLOOP3
       DEC   R5
       JNE   DLOOP4
DJUMP4 INC   R1
       MOV   R1,@SAVE5
       MOV   R9,@SAVE2
       MOV   R12,@SAVE1
       BL    *R9
       JMP   DLOOP3
       SBZ   >00
       LWPI  DREGS
       MOV   R9,R0
       BLWP  @VSBR$
       SRL   R1,13
       JNE   DJUMP7
       RTWP
*
DJUMP5 LWPI  DREGS
DJUMP6 CLR   R1
DJUMP7 SWPB  R1
       MOVB  R1,*R13
       SOCB  @HEX20,R15
       RTWP
       END
